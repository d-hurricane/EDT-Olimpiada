
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Перем ДокументРезультат;
	
	СгенерироватьПаролиУчастникам(ДокументРезультат);
	
	ДокументРезультат.Показать("Пароли участников");
	
	ПоказатьОповещениеПользователя(
		"Пароли установлены",
		"e1cib/list/РегистрСведений.Участники", 
		"Открыть список");
	
КонецПроцедуры

&НаСервере
Процедура СгенерироватьПаролиУчастникам(ДокументРезультат)

	ДокументРезультат = Новый ТабличныйДокумент;
	
	ДокументРезультат.Область(, 1, , 1).ШиринаКолонки = 25;
	ДокументРезультат.Область(, 2, , 2).ШиринаКолонки = 10;
	
	НомерСтроки = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Участники.Участник КАК Участник,
		|	Участники.Участник.Идентификатор КАК Идентификатор
		|ИЗ
		|	РегистрСведений.Участники КАК Участники
		|ГДЕ
		|	Участники.Инкогнито = ЛОЖЬ";
	
	ВыборкаУчастников = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаУчастников.Следующий() Цикл
		
		НовыйПароль = РегистрыСведений.Участники.НовыйПароль();
		
		Пользователь = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ВыборкаУчастников.Идентификатор);
		Пользователь.Пароль = НовыйПароль;
		Пользователь.Записать();
				
		НомерСтроки = НомерСтроки + 1;
		
		ДокументРезультат.Область(НомерСтроки, 1).Текст = Пользователь.ПолноеИмя;
		ДокументРезультат.Область(НомерСтроки, 2).Текст = НовыйПароль;
		
	КонецЦикла;
	
КонецПроцедуры