#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		ОбновитьСлужебныеРеквизиты();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбновитьСлужебныеРеквизиты();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПарольИзменен Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("НовыйПароль", Пароль);
	КонецЕсли;
	
	МассивРолей = Новый Массив;
	Для Каждого СтрокаРоли Из Роли Цикл
		Если СтрокаРоли.Использование Тогда
			МассивРолей.Добавить(СтрокаРоли.Роль);
		КонецЕсли;
	КонецЦикла;
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Роли", МассивРолей);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьСлужебныеРеквизиты();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Если Не ПустаяСтрока(Объект.Наименование) Тогда
		МассивСлов = СтрРазделить(Объект.Наименование, " ", Ложь);
		
		Объект.ИмяДляВхода = МассивСлов[0];
		Для Индекс = 1 По МассивСлов.ВГраница() Цикл
			Объект.ИмяДляВхода = Объект.ИмяДляВхода + ВРег(Лев(МассивСлов[Индекс], 1));
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьПароль(Команда)
	
	Обработчик = Новый ОписаниеОповещения("УстановитьПарольЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.Пользователи.Форма.ФормаВводПароля", , ЭтотОбъект, , , , Обработчик);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьПарольЗавершение(НовыйПароль, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(НовыйПароль) = Тип("Строка") Тогда
		ЭтотОбъект.Пароль = НовыйПароль;
		ЭтотОбъект.ПарольИзменен = Истина;
		ЭтотОбъект.ПарольУстановлен = Не ПустаяСтрока(НовыйПароль);
		
		ЭтотОбъект.Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьСлужебныеРеквизиты()
	
	Перем ПользовательИБ;
	
	Если ЗначениеЗаполнено(Объект.Идентификатор) Тогда
		ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(Объект.Идентификатор);
	КонецЕсли;
	
	ПарольИзменен = Ложь;
	Если ПользовательИБ <> Неопределено Тогда
		ПарольУстановлен = ПользовательИБ.ПарольУстановлен;
	КонецЕсли;
	
	Элементы.ГруппаПредупреждения.Видимость = (Не Объект.Ссылка.Пустая() И ПользовательИБ = Неопределено);
	
	Роли.Очистить();
	Для Каждого Роль Из Метаданные.Роли Цикл
		СтрокаРоли = Роли.Добавить();
		СтрокаРоли.Роль = Роль.Имя;
		СтрокаРоли.Представление = Роль.Представление();
		Если ПользовательИБ <> Неопределено Тогда
			СтрокаРоли.Использование = ПользовательИБ.Роли.Содержит(Роль);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти