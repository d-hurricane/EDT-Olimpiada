
#Область ПрограммныйИнтерфейс

Функция КодНаВстроенномЯзыке(ИсходныйКод) Экспорт
	
	ШаблонНачала = 
		"<HTML>
		|	<HEAD>
		|		<STYLE type=text/css>
		|			SPAN {FONT-SIZE: 8pt; FONT-FAMILY: 'Courier New'; COLOR: #0000ff}
		|				.PREPROC {COLOR: #963200}
		|				.COMMENT {COLOR: #008000}
		|				.KEYWORD {COLOR: #ff0000}
		|				.SNAME   {COLOR: #0000ff}
		|				.NUMBER  {COLOR: #000000}
		|				.STRING  {COLOR: #000000}
		|				.DATE    {COLOR: #000000}
		|				.LABEL   {COLOR: #000000}
		|				.SIGN    {COLOR: #ff0000}
		|		</STYLE>
		|	</HEAD>
		|	<BODY>
		|<PRE><SPAN>";
	ШаблонОкончания = 
		"</SPAN><PRE>
		|	</BODY>
		|</HTML>";
	
	ЧастиФорматированногоТекста = Новый Массив;
	ЧастиФорматированногоТекста.Добавить(ШаблонНачала);
		
	СтрокиИсходногоКода = СтрРазделить(ИсходныйКод, Символы.ПС, Истина);
	
	КлючевыеСлова = КлючевыеСловаВстроенногоЯзыка();
	
	ВсеПустыеСимволы = СтрокаПустыхСимволов();
	ВсеБуквы = СтрокаВсехБуквАлфавитов();
	ВсеЦифры = "0123456789";
	ВсеЦифрыСТочкой = ВсеЦифры + ".";
	ВсеБуквыИЦифры = ВсеБуквы + ВсеЦифры;
	
	ТипЛексемы = Неопределено;
	Лексема = Неопределено;
	
	Для ИндексПодстроки = 0 По СтрокиИсходногоКода.ВГраница() Цикл
		
		ПодстрокаКода = СтрокиИсходногоКода[ИндексПодстроки];
		ДлинаПодстроки = СтрДлина(ПодстрокаКода);
		
		НачалоЛексемы = 1;
		ПредыдущаяЛексема = "";
		
		ФорматированнаяПодстрока = ?(ИндексПодстроки = 0, "", Символы.ПС);
		
		Пока НачалоЛексемы <= ДлинаПодстроки Цикл
			
			КонецЛексемы = НачалоЛексемы + 1;
			СимволЛексемы = Сред(ПодстрокаКода, НачалоЛексемы, 1);
			ЗаменитьСлужебныеСимволы = Истина;
			
			Если СтрНайти(ВсеПустыеСимволы, СимволЛексемы) Тогда
				// пробельные символы
				ТипЛексемы = Неопределено;
				ПропуститьСимволыВСтроке(ПодстрокаКода, ВсеПустыеСимволы, КонецЛексемы, ДлинаПодстроки);
				ЗаменитьСлужебныеСимволы = Ложь;
				
			ИначеЕсли СтрНайти(ВсеБуквы, СимволЛексемы) Тогда
				// символьное имя
				ТипЛексемы = "SNAME";
				ПропуститьСимволыВСтроке(ПодстрокаКода, ВсеБуквыИЦифры, КонецЛексемы, ДлинаПодстроки);
				ЗаменитьСлужебныеСимволы = Ложь;
				
				Если ПредыдущаяЛексема <> "." Тогда
					Лексема = ВРег(Сред(ПодстрокаКода, НачалоЛексемы, КонецЛексемы - НачалоЛексемы));
					Если КлючевыеСлова[Лексема] = Истина Тогда
						ТипЛексемы = "KEYWORD";
					КонецЕсли;
				КонецЕсли; 
				
			ИначеЕсли (СимволЛексемы = "#" Или СимволЛексемы = "&") И ПустаяСтрока(ФорматированнаяПодстрока) Тогда 
				// инструкции препроцессора, разметка области
				ТипЛексемы = "PREPROC";
				КонецЛексемы = ДлинаПодстроки + 1;
				
			ИначеЕсли Сред(ПодстрокаКода, НачалоЛексемы, 2) = "//" Тогда 
				// комментарий
				ТипЛексемы = "COMMENT";
				КонецЛексемы = ДлинаПодстроки + 1; 
				
			ИначеЕсли СтрНайти(ВсеЦифры, СимволЛексемы) Тогда 
				// число
				ТипЛексемы = "NUMBER";
				ПропуститьСимволыВСтроке(ПодстрокаКода, ВсеЦифрыСТочкой, КонецЛексемы, ДлинаПодстроки);
				ЗаменитьСлужебныеСимволы = Ложь;
				
			ИначеЕсли СимволЛексемы = "~" Тогда
				// метка перехода
				ТипЛексемы = "LABEL";
				ПропуститьСимволыВСтроке(ПодстрокаКода, ВсеБуквыИЦифры, КонецЛексемы, ДлинаПодстроки);
				ЗаменитьСлужебныеСимволы = Ложь;
				
			ИначеЕсли СимволЛексемы = """" Или СимволЛексемы = "|" Тогда
				// строка
				ТипЛексемы = "STRING";
				ПропуститьСимволыДоЗакрывающихКавычек(ПодстрокаКода, КонецЛексемы, ДлинаПодстроки);
				
			ИначеЕсли СимволЛексемы = "'" Тогда
				// дата
				ТипЛексемы = "DATE";
				КонецЛексемы = СтрНайти(ПодстрокаКода, "'", , НачалоЛексемы + 1);
				Если КонецЛексемы = 0 Тогда
					КонецЛексемы = ДлинаПодстроки + 1;
				КонецЕсли; 
				
			Иначе
				// произвольный символ
				ТипЛексемы = "SIGN";
				
			КонецЕсли; 
			
			Лексема = Сред(ПодстрокаКода, НачалоЛексемы, КонецЛексемы - НачалоЛексемы);
			
			ТекстДляВставки = Лексема;
			Если ЗаменитьСлужебныеСимволы Тогда
				ЗаменитьСлужебныеСимволыHTML(ТекстДляВставки);
			КонецЕсли; 
			Если ТипЛексемы <> Неопределено И ТипЛексемы <> "SNAME" Тогда
				ТекстДляВставки = СтрШаблон("<SPAN class=%1>%2</SPAN>", ТипЛексемы, ТекстДляВставки);
			КонецЕсли;
			
			ФорматированнаяПодстрока = ФорматированнаяПодстрока + ТекстДляВставки;
			
			ПредыдущаяЛексема = Лексема;				
			НачалоЛексемы = КонецЛексемы;
			
		КонецЦикла;
		
		ЧастиФорматированногоТекста.Добавить(ФорматированнаяПодстрока);
				
	КонецЦикла;
	
	ЧастиФорматированногоТекста.Добавить(ШаблонОкончания);	
	Возврат СтрСоединить(ЧастиФорматированногоТекста);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПропуститьСимволыВСтроке(ИсходнаяСтрока, ПропускаемыеСимволы, ТекущаяПозиция, Ограничение)
	
	Пока ТекущаяПозиция <= Ограничение 
		И СтрНайти(ПропускаемыеСимволы, Сред(ИсходнаяСтрока,ТекущаяПозиция,1)) Цикл
		
		ТекущаяПозиция = ТекущаяПозиция + 1;
		
	КонецЦикла;
		
КонецПроцедуры

Процедура ПропуститьСимволыДоЗакрывающихКавычек(ИсходнаяСтрока, ТекущаяПозиция, Ограничение)
	
	ПродолжатьПоискКонцаСтроки = (ТекущаяПозиция <= Ограничение);	
	Пока ПродолжатьПоискКонцаСтроки Цикл
		
		ТекущаяПозиция = СтрНайти(ИсходнаяСтрока, """", , ТекущаяПозиция);
		
		Если ТекущаяПозиция = Ограничение Или ТекущаяПозиция = 0 Тогда
			ТекущаяПозиция = Ограничение + 1;
			ПродолжатьПоискКонцаСтроки = Ложь;
		ИначеЕсли Сред(ИсходнаяСтрока, ТекущаяПозиция+1, 1) = """" Тогда
			ТекущаяПозиция = ТекущаяПозиция + 2;
			ПродолжатьПоискКонцаСтроки = (ТекущаяПозиция <= Ограничение);
		Иначе 
			ТекущаяПозиция = ТекущаяПозиция + 1;
			ПродолжатьПоискКонцаСтроки = Ложь;
		КонецЕсли; 
		
	КонецЦикла;
				
КонецПроцедуры

Процедура ЗаменитьСлужебныеСимволыHTML(Текст)
	
	Текст = СтрЗаменить(Текст, "&", "&amp;");
	Текст = СтрЗаменить(Текст, "<", "&lt;");
	Текст = СтрЗаменить(Текст, ">", "&gt;");
				
КонецПроцедуры

Функция КлючевыеСловаВстроенногоЯзыка()
	
	МассивСлов = "
	|Функция
	|КонецФункции	
	|Процедура
	|КонецПроцедуры
	|Экспорт
	|Знач
	|Перем
	
	|Function
	|EndFunction	
	|Procedure
	|EndProcedure
	|Export
	|Val
	|Var
	
	|Если
	|Тогда
	|ИначеЕсли
	|Иначе
	|КонецЕсли
	
	|If
	|Then
	|ElsIf
	|Else
	|EndIf
	
	|Пока
	|Для
	|Каждого
	|По
	|Цикл
	|КонецЦикла
	
	|While
	|For
	|Each
	|To
	|Do
	|EndDo
	
	|Возврат
	|Прервать
	|Продолжить
	|Перейти
	
	|Return
	|Break
	|Continue
	|GoTo
	
	|Выполнить
	|ДобавитьОбработчик
	|УдалитьОбработчик
	
	|Execute
	|AddHandler
	|RemoveHandler
	
	|Попытка
	|Исключение
	|КонецПопытки
	|ВызватьИсключение
	
	|Try
	|Except
	|EndTry
	|Raise
	
	|Новый
	|Неопределено
	|NULL
	
	|New
	|Undefined
	
	|И
	|Или
	|Не
	|Истина
	|Ложь
	
	|And
	|Or
	|Not
	|True
	|False
	
	|";
	
	КлючевыеСлова = Новый Соответствие;
	Для каждого Слово Из СтрРазделить(ВРег(МассивСлов), Символы.ПС, Ложь) Цикл
		КлючевыеСлова.Вставить(СокрЛП(Слово), Истина);
	КонецЦикла; 
	
	Возврат КлючевыеСлова;
	
КонецФункции

Функция СтрокаПустыхСимволов()
	
	Возврат " " + Символы.ВК + Символы.ВТаб + Символы.НПП + Символы.ПС + Символы.ПФ + Символы.Таб;
	
КонецФункции

Функция СтрокаВсехБуквАлфавитов()
	
	ВсеБуквы = "АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯABCDEFGHIJKLMNOPQRSTUVWXYZ";
	Возврат ВсеБуквы + "_" + НРег(ВсеБуквы);
	
КонецФункции

#КонецОбласти